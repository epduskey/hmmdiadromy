model{
	
	# Priors for transition matrix
	for(i in 1:nstates) {
		for(j in 1:nstates) {
			a[i,j] ~ dgamma(1,1)	
		}
		trn[i,1:nstates] ~ ddirich(a[i,1:nstates] + 0.1)
	}
	
	# Estimate states from transition matrix
	for(i in 1:nfish) {
		for(j in 2:ndays) {
			s[i,j] ~ dcat(trn[s[i,j-1], ])
		}
	}
	
	# Estimate detection probability
	for(i in 1:nfish) {
		for(j in 1:ndays) {
			search[i,j] <- ifelse(rg[s[i,j],j] == 1, 1, 0)
			det[i,j] ~ dbin(p*search[i,j], 1)
		}
	}
	
	# Detection prior
	p ~ dunif(0,1)
	
	# Estimate occupancy at the substate scale
	for(i in 1:nstates) {
		for(j in 1:pcs) {
			# Logit link for occupancy probability as a function of sand
			logit(pocc[i,j]) <- a0 + a1*sand[i,j]	
		}
	}
	for(i in 1:nfish) {
		for(j in 1:ndays) {
			# Grid scale occupancy drawn from categorical
			grid[i,j] ~ dcat(pocc[s[i,j],])
		}
	}
	
	# Store both state and substate estimates
	out = s*pcs - (pcs-grid)
	
	# Logistic regression priors - occupancy
	a0 ~ dnorm(0, 0.001)
	a1 ~ dnorm(0, 0.001)
	
}